getValue(oo,ex,h,%db) ; Выдает значение:
 ; Если открыта транзакция, то текущее (либо из ^..D либо из Журнала)
 ; Если режим - отчет по SnapShot, то из журнала SS или из БД
 ; Иначе - из БД.
 n z
 ;
 ; Для транзакции...
 i '$g(%db),$g(%IxStat(0)) d  q $s($d(z):z,1:$s(h'<0:$s(Lang="R":$g(@("^"_oo_"D")@(ex,h)),1:$s($d(@("^"_oo_"D")@(ex,h,Lang)):^(Lang),1:$g(@("^"_oo_"D")@(ex,h)))),1:$$GetVirt()))
 .n r
 .i $g(^IxDB(%IxStat(1),oo,ex))=0 s z="" q  ; эк-р в транзакции удален
 .i $g(^IxDB(%IxStat(1),oo,ex))=1 s z=$g(^IxDB(%IxStat(1),oo,ex,1,h)) q  ; если создан в транзакции, то берем имеенно из журнала
 .q:'$d(^IxDB(%IxStat(1),oo,ex,1,h))  ;       зна-я в журнале нет
 .s z=^(h) q  ;                               иначе - выдаем из журнала
 ;
 ; Для SnapShot...
 i '$g(%db),$g(%IxStat(5.5))'="" d  q $s($d(z):z,1:$s(h'<0:$s(Lang="R":$g(@("^"_oo_"D")@(ex,h)),1:$s($d(@("^"_oo_"D")@(ex,h,Lang)):^(Lang),1:$g(@("^"_oo_"D")@(ex,h)))),1:$$GetVirt()))
 .n r
 .s r=$o(^IxSS(2,oo,ex,1,%IxStat(5.6))) ; статус эк-ра до транзакции, после которой начался отчет
 .q:r=""  ;             эк-р не менялся
 .i ^(r)=0 s z="" q  ;  статус определен и эк-ра не было
 .; эк-р ПРИСУТСВОВАЛ до нашей транзакции - выдаем "старое" значение, если оно есть
 .s r=$o(^IxSS(2,oo,ex,2,h_$s(Lang="R":"",1:"$"_Lang),%IxStat(5.7))) ; номер транзакции, в которой было изменено значение
 .q:r=""  ;  нет такой транзакции
 .s z=^(r) ; значение было другое - выдаем его
 .q
 ;
 q $s(h'<0:$s(Lang="R":$g(@("^"_oo_"D")@(ex,h)),1:$s($d(@("^"_oo_"D")@(ex,h,Lang)):^(Lang),1:$g(@("^"_oo_"D")@(ex,h)))),1:$$GetVirt())  ; иначе - выдаем значение из файла данных
